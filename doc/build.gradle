/*******************************************************************************
 * Copyright (c) 2018 - Prevo-System AG and others.
 * 
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 * 
 * This Source Code may also be made available under the following Secondary
 * Licenses when the conditions for such availability set forth in the Eclipse
 * Public License, v. 2.0 are satisfied: GNU General Public License, version 3
 * with the GNU Classpath Exception which is
 * available at https://www.gnu.org/software/classpath/license.html.
 * 
 * SPDX-License-Identifier: EPL-2.0 OR GPL-3.0 WITH Classpath-exception-2.0
 * 
 * Contributors:
 *     Prevo-System AG - initial API and implementation
 ******************************************************************************/

buildscript {
    repositories {
        maven {
            name 'Bintray Asciidoctor repo'
            url 'http://dl.bintray.com/content/aalmiray/asciidoctor'
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        classpath("org.asciidoctor:asciidoctor-gradle-plugin:1.5.6")
        classpath("org.ajoberstar:gradle-git-publish:0.3.0")
    }
}


ext {
    srcDir = "$projectDir/arc42"
    targetDir = "$buildDir/docs"
    currentDate = new Date().format("MMM d. yyyy")
}

version = '0.0.1-SNAPSHOT'

// =========== asciidoctor ===============
apply plugin: 'org.asciidoctor.convert'

asciidoctor {
    outputDir = file(targetDir)
    sourceDir = file(srcDir)
    sources { include "index.adoc" }
    attributes = [
            separateOutputDirs  : false,
            imagesdir           : 'images',
            doctype             : 'book',
            icons               : 'font',
            toc                 : 'left',
            sectlink            : true,
            sectanchors         : true,
            numbered            : true,
            'source-highlighter': 'coderay',
            'coderay-css'       : 'class',
            stylesheet          : 'style.css',
            stylesdir           : "$srcDir/css/",
            version             : project.version,
            'currentDate'       : "$currentDate"
    ]

    resources {
        from("${srcDir}/") {
            include 'images/**'
        }
    }
}

// =========== publishing ===============
apply plugin: 'org.ajoberstar.git-publish'

gitPublish {
    System.setProperty('org.ajoberstar.grgit.auth.username', System.getenv('GH_TOKEN') ?: "")
    repoUri = 'https://github.com/open-prevo/openprevo.git'
    branch = 'gh-pages'

    contents {
        from('build/docs/html5')
    }
}

gitPublishPush.dependsOn asciidoctor

task publishDocumentation(type: GradleBuild) {
    // Since the variable `TRAVIS_BRANCH` points to "master" if there is an open PR we need to filter that as well
    if (!System.getenv('TRAVIS_PULL_REQUEST_BRANCH') && System.getenv('TRAVIS_BRANCH') == "master") {
        tasks = ['gitPublishPush']
    } else {
        println 'will not publish on gh-pages'
    }
}
