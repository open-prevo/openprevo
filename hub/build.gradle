apply plugin: 'java'

apply plugin: 'org.springframework.boot'

dependencies {
    compile(project(":encrypted-data-model"))

    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "spring-boot-starter-tomcat"
    }

    compile("javax.inject:javax.inject")
    compile("org.springframework.boot:spring-boot-starter-jetty")
    compile("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml")

    testCompile("org.testcontainers:testcontainers")
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("org.assertj:assertj-core")
}

test {
    // exclude integration tests by default
    exclude 'ch/prevo/open/hub/integration/**'
    finalizedBy 'ciIntegrationTesting'
}

task ciIntegrationTesting(type: GradleBuild) {
    tasks = ['integrationTest']
    onlyIf {
        System.getenv("TRAVIS") == "true"
    }
}

task integrationTest(type: Test) {
    include 'ch/prevo/open/hub/integration/**'
}

integrationTest.dependsOn { project(":node").tasks.docker }
