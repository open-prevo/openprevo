import org.apache.tools.ant.filters.*

apply plugin: 'com.palantir.docker'
apply plugin: 'docker-compose'

task buildDemoZip(type: Zip) {
    from "${buildDir}/demo"
    include('**/*')
    exclude('**/demo-nodes-docker.yml')
    archiveName 'demo.zip'
    destinationDir file("${buildDir}/")
}

task prepareHubDockerConfig(type: Copy) {
    from("demo-nodes.yml")

    filter { line ->
        line.replaceAll('localhost:8851', 'node_baloise:8851')
            .replaceAll('localhost:8852', 'node_helvetia:8852')
            .replaceAll('localhost:8853', 'node_zurich:8853')
    }

    rename("demo-nodes.yml", "demo-nodes-docker.yml")
    into "${buildDir}/demo/Hub"
}

task prepareHub(dependsOn: prepareHubDockerConfig, type: Copy) {
    from("${project(':hub').buildDir}/libs")
    from("demo-nodes.yml")
    into "${buildDir}/demo/Hub"
    doLast {
        def javaCmd = "java " +
                "-Dserver.port=8859 " +
                "-Dopen.prevo.hub.config.file=file:demo-nodes.yml " +
                "-Dspring.profiles.active=demo " +
                "-jar ${project(":hub").bootJar.archiveName}"
        def startBatFile = new File("${buildDir}/demo/Hub/start.bat")
        startBatFile.text = "start \"OpenPrevo - demo hub\" "  + javaCmd
        startBatFile.executable = true
        def cleanBatFile = new File("${buildDir}/demo/Hub/clean-db.bat")
        cleanBatFile.text = 'del openprevo.mv.db'
        cleanBatFile.executable = true
        def startShFile = new File("${buildDir}/demo/Hub/start.sh")
        startShFile.text = "#!/usr/bin/env bash\n${javaCmd}"
        startShFile.executable = true
        def cleanDbShFile = new File("${buildDir}/demo/Hub/clean-db.sh")
        cleanDbShFile.text = '#!/usr/bin/env bash\nrm openprevo.mv.db'
        cleanDbShFile.executable = true
    }
}

buildDemoZip.dependsOn(prepareHub)
prepareHub.dependsOn { project(":hub").bootJar }

// Define Node Configuration Here
// For each configuration gradle will automatically create a prepare${NodeName} task
// which copies all files needed into a sub-directory named the same as the node

def nodeConfiguration = [
        [name: "NodeBaloiseExcel", adapter: "adapter-excel", port: "8851"],
        [name: "NodeBaloisePakt", adapter: "adapter-pakt", port: "8851"],
        [name: "NodeHelvetiaExcel", adapter: "adapter-excel", port: "8852"],
        [name: "NodeZurichExcel", adapter: "adapter-excel", port: "8853"]
]

def createTask(node) {
    task "prepare${node.name}"(type: Copy) {

        dependsOn { project(":${node.adapter}").shadowJar }
        dependsOn { project(":node").bootJar }

        from(project(":${node.adapter}").shadowJar)
        from("${project(':node').buildDir}/libs")

        if (node.adapter == "adapter-excel") {
            from("src/data/${node.name}-Input.xlsx")
        } else if (node.adapter == "adapter-pakt") {
            from fileTree("src/adapter-pakt")
        }

        from("src/data/${node.name}-config.yml")

        into "${buildDir}/demo/${node.name}"

        doLast {
            def javaCmd = getJavaCommandLineForNode(node)
            def batFile = new File("${buildDir}/demo/${node.name}/start.bat")
            batFile.text = "start \"OpenPrevo - demo node ${node.name}\" " + javaCmd
            batFile.executable = true
            def shFile = new File("${buildDir}/demo/${node.name}/start.sh")
            shFile.text = "#!/usr/bin/env bash\n${javaCmd}"
            shFile.executable = true
        }
    }
    buildDemoZip.dependsOn("prepare${node.name}")
}

nodeConfiguration.each { node ->
    createTask(node)
}

def getJavaCommandLineForNode(nodeConfiguration) {

    def cmd = "java " +
            "-Dloader.path=${project(':' + nodeConfiguration.adapter).shadowJar.archiveName},config " +
            "-Dserver.port=${nodeConfiguration.port} " +
            "-Dspring.profiles.active=demo " +
            "-Dopen.prevo.node.config.file=file:${nodeConfiguration.name}-config.yml"

    if (nodeConfiguration.adapter == "adapter-excel") {
        cmd += " -Dnode.adapter.excel.in.file=${nodeConfiguration.name}-Input.xlsx"
    }
    cmd += " -jar ${project(":node").bootJar.archiveName}"
}

assemble.dependsOn(buildDemoZip)

docker {
    dependsOn buildDemoZip
    name "openprevo/node-demo-base:staging"
}

dockerCompose {
    useComposeFiles = ['docker-compose.yml']
    waitForTcpPorts = true
    stopContainers = true
    removeContainers = true
}

composeUp.dependsOn { tasks.docker }

task demoIntegrationTest {
    dependsOn composeUp
    doLast {
        // wait till the matching runs through
        sleep(20000)

        //assert that all nodes are notified with the expected amount of matches
        assert 10 == new File("${buildDir}/demo/NodeBaloiseExcel").listFiles().findAll {
            it.name.startsWith("retirement-fund-out-data")
        }.size()
        assert 18 == new File("${buildDir}/demo/NodeHelvetiaExcel").listFiles().findAll {
            it.name.startsWith("retirement-fund-out-data")
        }.size()
        assert 8 == new File("${buildDir}/demo/NodeZurichExcel").listFiles().findAll {
            it.name.startsWith("retirement-fund-out-data")
        }.size()
    }
}

demoIntegrationTest.finalizedBy composeDown

task ciDemoIntegrationTesting(type: GradleBuild) {
    tasks = ['demoIntegrationTest']
    onlyIf {
        System.getenv("TRAVIS") == "true"
    }
}

check.finalizedBy ciDemoIntegrationTesting
